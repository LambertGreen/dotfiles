#!/usr/bin/env just
# -*- mode: just -*-

# Justfile for common POSIX system-level maintenance tasks.
# Shared across macOS, Linux, and WSL (for systems using Homebrew).

# * Homebrew
# ────────────────────────────────────────────────────────────────
# @category Homebrew

# 🔎 Update taps, then preview which formulae and casks are outdated
brew-check:
    @echo "🔄 Updating Homebrew metadata..."
    brew update
    @echo
    @echo "🔎 Checking outdated Homebrew formulae..."
    brew outdated
    @echo
    @echo "🔎 Checking outdated Homebrew casks..."
    brew outdated --cask --greedy

# 📦 Upgrade installed formulae and casks (no implicit brew update)
brew-upgrade:
    @echo "📦 Upgrading formulae and casks..."
    brew upgrade
    brew upgrade --cask --greedy

# 🧹 Clean cache and remove orphaned dependencies
brew-clean:
    @echo "🧹 Cleaning Homebrew cache and unused dependencies..."
    brew cleanup -s
    brew autoremove

# 🧰 Diagnose Homebrew installation
brew-doctor:
    @echo "🧰 Running brew doctor..."
    @sh -c 'brew doctor || echo "⚠️  brew doctor returned warnings (exit code $?). Continuing..."'

# 🛠️ Monthly deep maintenance (check → upgrade → clean → doctor)
brew-maintain: brew-check brew-upgrade brew-clean brew-doctor

# * Zinit
# ────────────────────────────────────────────────────────────────
# @category Zinit

# 🧃 Update zinit core and all plugins
zinit-update:
    @echo "🧃 Updating zinit core and plugins..."
    zsh -ic 'zinit self-update && zinit update --all'

# 🧃 Check zinit setup and loaded plugins
zinit-check:
    @echo "🧃 Checking zinit availability and plugin status..."
    zsh -ic 'zinit report || echo "⚠️  Zinit not found or not loaded properly."'

# * Completions
# ────────────────────────────────────────────────────────────────
# @category Completions

# 🖥️ Refresh all completions
completions-upgrade:
    @echo "🖥️ Refreshing just completions..."
    just --completions zsh > ~/.config/zsh/completions/_just

    @echo "🖥️ Refreshing wezterm completions..."
    wezterm shell-completion --shell zsh > ~/.config/zsh/completions/_wezterm

# * Emacs
# ────────────────────────────────────────────────────────────────
# @category Emacs

# 📝 Update Emacs Elpaca packages in batch mode
emacs-upgrade:
    @echo "📝 Updating Emacs Elpaca packages..."
    DOTFILES_EMACS_UPDATE=1 emacs --batch --init-directory=~/.emacs.default/ --load=~/.emacs.default/init.el
