#!/usr/bin/env just
# -*- mode: just -*-

# Justfile for system-level maintenance tasks
import '~/.justfile_common'
import '~/.justfile_posix'

# * Default
# ──────────────────────────────────
default:
    @just --list

# * Mac App Store
# ────────────────────────────────────────────────────────────────
# @category Mac-App-Store

# 🔎 Preview which Mac App Store apps are outdated
mas-check:
    @echo "🔎 Checking for Mac App Store updates..."
    mas outdated

# 📦 Upgrade all Mac App Store apps
mas-upgrade:
    @echo "📦 Upgrading Mac App Store apps..."
    mas upgrade

# * macOS System Updates
# ────────────────────────────────────────────────────────────────
# @category Mac

# 🧭 Check available macOS software updates
os-check:
    @echo "🧭 Checking for macOS updates..."
    softwareupdate -l

# 🛠️ Apply all available macOS software updates
os-update:
    @echo "🛠️ Installing all available macOS updates (requires sudo)..."
    sudo softwareupdate -ia

# 🧰 Check Xcode Command Line Tools
xcode-check:
    @echo "🧰 Checking for Xcode Command Line Tools..."
    @sh -c 'if xcode-select -p > /dev/null 2>&1; then \
      echo "✅ Xcode CLT is installed at: $(xcode-select -p)"; \
    else \
      echo "❌ Xcode CLT not installed. Run: xcode-select --install"; \
    fi'

# * System
# ────────────────────────────────────────────────────────────────
# @category System

# 🔎 Preview system updates without applying them
system-check: brew-check mas-check os-check xcode-check

# 🛠️ Fast weekly system update (for Mondays)
system-update: brew-upgrade mas-upgrade completions-update

# 🧹 Monthly system maintenance (includes cleanup, doctor, and app store)
system-maintain: brew-maintain mas-upgrade zinit-update emacs-update os-update xcode-check
